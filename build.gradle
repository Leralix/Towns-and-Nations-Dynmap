plugins {
    id 'java'
}

subprojects {
    apply plugin: 'java'
    task copyPluginYml(type: Copy) {
        from "$projectDir/src/main/resources/plugin.yml"
        into "$buildDir/resources/main"
    }
    jar {
        from rootProject.sourceSets.main.output
    }

}

allprojects {
    group = 'org.leralix'
    version = '0.10.0'

    repositories {
        mavenCentral()
        maven {
            name = "spigotmc-repo"
            url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/groups/public/"
        }
        flatDir {
            dirs "$rootDir/libs"
        }
    }

    dependencies {
        compileOnly "org.spigotmc:spigot-api:1.20.2-R0.1-SNAPSHOT"
        compileOnly name: 'Towns_and_Nations-0.11.8'
    }

    def targetJavaVersion = 17
    java {
        def javaVersion = JavaVersion.toVersion(targetJavaVersion)
        sourceCompatibility = javaVersion
        targetCompatibility = javaVersion
        if (JavaVersion.current() < javaVersion) {
            toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
        }
    }

    tasks.withType(JavaCompile).configureEach {
        if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
            options.release.set(targetJavaVersion)
        }
    }
    processResources {
        def props = [version: version]
        inputs.properties props
        filteringCharset 'UTF-8'
        filesMatching('plugin.yml') {
            expand props
        }
    }
}





